cmake_minimum_required(VERSION 3.19)

set(Dynemu_known_comps static shared)
set(Dynemu_comp_static NO)
set(Dynemu_comp_shared NO)
foreach (Dynemu_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (Dynemu_comp IN_LIST Dynemu_known_comps)
        set(Dynemu_comp_${Dynemu_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Dynemu does not recognize component `${Dynemu_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (Dynemu_comp_static AND Dynemu_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "Dynemu `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(Dynemu_static_targets "${CMAKE_CURRENT_LIST_DIR}/Dynemu-static-targets.cmake")
set(Dynemu_shared_targets "${CMAKE_CURRENT_LIST_DIR}/Dynemu-shared-targets.cmake")

macro(Dynemu_load_targets type)
    if (NOT EXISTS "${Dynemu_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Dynemu `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${Dynemu_${type}_targets}")
endmacro()

if (Dynemu_comp_static)
    Dynemu_load_targets(static)
elseif (Dynemu_comp_shared)
    Dynemu_load_targets(shared)
elseif (DEFINED Dynemu_SHARED_LIBS AND Dynemu_SHARED_LIBS)
    Dynemu_load_targets(shared)
elseif (DEFINED Dynemu_SHARED_LIBS AND NOT Dynemu_SHARED_LIBS)
    Dynemu_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${Dynemu_shared_targets}")
        Dynemu_load_targets(shared)
    else ()
        Dynemu_load_targets(static)
    endif ()
else ()
    if (EXISTS "${Dynemu_static_targets}")
        Dynemu_load_targets(static)
    else ()
        Dynemu_load_targets(shared)
    endif ()
endif ()